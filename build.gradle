buildscript {
    ext.kotlin_version = "1.3.70"
    repositories {
		mavenCentral()
		maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
	}
}
plugins {
    id "org.jetbrains.intellij" version "1.1.2"
	id "org.jetbrains.kotlin.jvm" version "1.3.70"
}
apply plugin:"java"
apply plugin:"kotlin"
apply plugin:"idea"
apply plugin:"org.jetbrains.intellij"

repositories {
    mavenCentral()
}

sourceSets {
    main {
        kotlin.srcDir "src"
        resources.srcDir "resources"
    }
    test {
        kotlin.srcDir "test"
    }
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
        apiVersion = "1.3"
        languageVersion = "1.3"
    }
}

intellij {
	// See https://www.jetbrains.com/intellij-repository/snapshots for available snapshots.
    // See https://plugins.jetbrains.com/docs/intellij/kotlin.html#kotlin-standard-library for the lowest bundled kotlin stdlib version.
    version = System.getenv().getOrDefault("OKEYDOKE_PLUGIN_IDEA_VERSION",
		"IC-202.7660.26" // This IJ version because it has "java" plugin extracted from core.
        // "212.4638-EAP-CANDIDATE-SNAPSHOT" // Version which is close to LATEST-EAP but won't be constantly re-downloaded.
        // "LATEST-EAP-SNAPSHOT"
	)
    pluginName = "okey-doke-idea"
    downloadSources = true
    sameSinceUntilBuild = false
    updateSinceUntilBuild = false
	plugins = ["java", "Kotlin", "junit"]
}

publishPlugin {
    token = System.getenv("ORG_GRADLE_PROJECT_intellijPublishToken")
}